// Copyright 2018 The Golem Language Authors. All rights reserved.
// Use of this source code is governed by a MIT-style
// license that can be found in the LICENSE file.

import os, regexp, path

fn searchFile(name, rgx) {
    let n = 1
    for line in os.open(name).readLines() {
        if rgx.matchString(line) {
            println([name, n, line].join(':'))
        }
        n++
    }
}

fn main(args) {

    let numArgs = len(args)
    if numArgs < 2 || numArgs > 3 {
        println('usage:')
        println('  extgrep.glm <dir> <string> <exts>')
        println(' ')
        println('example:')
        println('  extgrep.glm . Foo go,js,html,css')
        os.exit(1)
    }

    let dir = args[0]
    let rgx = regexp.compile(args[1])
    let exts = (numArgs == 3) ? 
        args[2].split(',') : 
        ['go']

    path.filepath.walk(dir, fn(name, info) {
        if !info.isDir {
            let ext = path.filepath.ext(name)
            if len(ext) > 0 && exts.contains(ext[1:]) {
                searchFile(name, rgx)
            }
        }
    })

    os.exit(0) 
}
 
